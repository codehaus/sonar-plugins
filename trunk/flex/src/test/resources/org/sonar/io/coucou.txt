      *-----------------------------------------------------------------
      *   PROGRAMME DE MISE EN FORME DES FLUX ADEI   POUR ODET         *
      *-----------------------------------------------------------------

      *-----------------------------------------------------------------
      * MAINTENANCE
      *-----------------------------------------------------------------
      *-----------------------------------------------------------------
       IDENTIFICATION DIVISION.
       PROGRAM-ID. P5FE03.
      *DATE-WRITTEN. 31/07/07.

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.

      * FICHIER EDITIONS SEPIAS
             SELECT FIC-E1     ASSIGN TO E1
             ORGANIZATION SEQUENTIAL
             FILE STATUS FS-E1.


      * ENREGISTREMENTS FLUX SEPIAS
             SELECT FIC-S1     ASSIGN TO S1
             ORGANIZATION SEQUENTIAL
             FILE STATUS FS-S1.


       DATA DIVISION.
       FILE SECTION.
      *
       FD  FIC-E1
               LABEL  RECORDS  STANDARD
               RECORDING MODE IS V
               BLOCK CONTAINS 0 RECORDS
               DATA RECORD IS ENR-E1.
       01  ENR-E1.
        03 ENR-E1-ENTETE PIC X(79).
        03 ENR-E1-DATA   PIC X(27915).

       FD  FIC-S1 LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS V.
       01  ENR-S1 PIC X(5002).
      *
       WORKING-STORAGE SECTION.

      ********************************************************
      *--- ZONE WORKING
      ********************************************************

      * DESCRIPTION ENREG ENTETE FICHIER ENTREE
        COPY Y5FNLNR.

      * DESCRIPTION ENREG FICHIER SORTIE
        COPY Y00ODETF.

      *--- RUPTURE STRUCTURE
       01 WS-NO-STR-SAVE    PIC X(6) VALUE SPACE.

      *--- NUMERO DU FICHIER PDF
       01  WS-NUM-EDI             PIC 9(05).

      *========================================================
      *  ACCESSEURS
      *========================================================
      *COPY Y00BIA.
      *    05    FILLER              PIC  X(3700).

       01 WS-RETURN-CODE    PIC 99 VALUE ZERO.

      * FILE STATUS
       01  FS-E1                       PIC XX.
       01  FS-S1                       PIC XX.

      * FLAG ANOMALIE
       01  FLAG-ANOMALIE        PIC X VALUE ZERO.
           88  FLAG-ANO        VALUE '1'.
           88  NOT-ANO         VALUE '0'.

      *
       01  FIN-E1                      PIC X  VALUE  '0'.
           88 FE1                      VALUE '1'.
      *
      *  SYSIN
       01 WS-SYSIN              PIC X(80).
       01 WS-URL-WEBSTORE       PIC X(60).
       01 WS-IND-NON-SPACE-DEB  PIC 99.
       01 WS-IND-NON-SPACE-FIN  PIC 99.

       01 WS-ADRESSE            PIC X(09).

      * --- DATE DU JOUR (SYSIN)
       01  WS-DATE-OPC.
           05  WS-DATE-OPC-SS     PIC X(02).
           05  WS-DT-OPC.
               10 WS-DATE-OPC-AA  PIC X(02).
               10 WS-DATE-OPC-MM  PIC X(02).
               10 WS-DATE-OPC-JJ  PIC X(02).

      *
      /------------------------------------------------
      *   DESCRIPTION LIGNES D'EDITION CHIFFRIER      *
      *------------------------------------------------
      *
       01  WS-CHIFFRIER.
           03  NB-COMPTEURS      PIC S9(4) COMP-3  VALUE +02.
           03  F                 PIC X(8)          VALUE 'P5FE03'.
           03  F                 PIC X(2)          VALUE '  '.
           03  F.
               05 F   PIC X(46)     VALUE 'NB ENREG ADEI LUS : '.
               05 F   PIC X         VALUE '3'.
               05 WS-NBRE-E1-LUS    PIC S9(15)   COMP-3   VALUE +0.
               05 F   PIC X(16).
           03  F.
               05 F   PIC X(46)     VALUE 'NB ENREG LIGNE ECRITES : '.
               05 F   PIC X         VALUE '3'.
               05 WS-NBRE-S1-ECR     PIC S9(15)   COMP-3   VALUE +0.
               05 F   PIC X(16).
      *
      ******************************************************
      *----- FIN ZONE WORKING
      ******************************************************
      *
      *
       PROCEDURE DIVISION.
       DEBUT.
      *
           ACCEPT WS-SYSIN     FROM SYSIN.

           IF WS-URL-WEBSTORE = SPACE
            DISPLAY '********************************************'
            DISPLAY 'ERREUR LORS DE LA RECUPERATION URL WEBSTORE '
            DISPLAY '********************************************'
            MOVE 12 TO WS-RETURN-CODE
            SET FLAG-ANO TO TRUE
           END-IF

           IF NOT-ANO
             PERFORM VARYING WS-IND-NON-SPACE-DEB FROM 1 BY +1
              UNTIL WS-SYSIN(WS-IND-NON-SPACE-DEB:1) NOT = SPACE
                        OR WS-IND-NON-SPACE-DEB = 80
             END-PERFORM

             MOVE WS-SYSIN(WS-IND-NON-SPACE-DEB:08) TO WS-DATE-OPC
             ADD 8 TO WS-IND-NON-SPACE-DEB
             MOVE WS-SYSIN(WS-IND-NON-SPACE-DEB:60) TO WS-URL-WEBSTORE

             PERFORM VARYING WS-IND-NON-SPACE-FIN FROM 60 BY -1
              UNTIL WS-URL-WEBSTORE (WS-IND-NON-SPACE-FIN:1) NOT = SPACE
                        OR WS-IND-NON-SPACE-FIN = 1
             END-PERFORM
           END-IF

           IF NOT-ANO
              PERFORM D-OUVERTURE      THRU F-OUVERTURE
           END-IF

           IF NOT-ANO
              PERFORM D-LECT-E1        THRU F-LECT-E1
           END-IF
      *
           IF NOT-ANO
              PERFORM D-TRAITEMENT THRU F-TRAITEMENT
                      UNTIL FLAG-ANO OR  FE1
           END-IF

      * ECRITURE DU CHIFFRIER
           IF NOT-ANO
             CALL 'P6FD99' USING WS-CHIFFRIER
           END-IF

           PERFORM D-FERMETURE      THRU  F-FERMETURE

           MOVE WS-RETURN-CODE TO RETURN-CODE
           .

       FIN-PROGRAMME.
           STOP RUN.

      *
       D-OUVERTURE.
      *------------------------------------------------------------
      *              OUVERTURE  FICHIERS
      *------------------------------------------------------------
           OPEN INPUT FIC-E1
           IF FS-E1 NOT = '00'
            DISPLAY '****************************************'
            DISPLAY 'OUVERTURE E1 INCORRECTE,STATUS : ' FS-E1
            DISPLAY '****************************************'
            MOVE 12 TO WS-RETURN-CODE
            SET FLAG-ANO TO TRUE
           END-IF
      *
           IF NOT-ANO
             OPEN OUTPUT FIC-S1
             IF FS-S1 NOT = '00'
              DISPLAY '****************************************'
              DISPLAY 'OUVERTURE S1 INCORRECTE,STATUS : '
                                           FS-S1
              DISPLAY '****************************************'
              MOVE 12 TO WS-RETURN-CODE
              SET FLAG-ANO TO TRUE
             END-IF
           END-IF.

       F-OUVERTURE.
           EXIT.
      *
       D-TRAITEMENT.

           IF NOT-ANO
      *-------PREPARATION DE LA LIGNE DES PARAMETRES D'EDITION
              MOVE SPACE TO Y00ODETF-ENR-ENTETE
              MOVE 'DB' TO Y00ODETF-CD-ENR-E
              MOVE '007' TO Y00ODETF-CD-FCT
              MOVE '11'  TO Y00ODETF-CD-EFS
              MOVE '12345678'   TO Y00ODETF-NO-PSE

              MOVE Y5FNLN-NB-EX      TO Y00ODETF-NB-EX
              MOVE Y5FNLN-NO-STR-OPE TO Y00ODETF-NO-STR
              MOVE Y5FNLN-CD-REF-DOC TO Y00ODETF-CD-DOC-EDI
      *      --- infos pour archivage automatique (GED SECHAT) du pdf
              IF Y5FNLN-NO-DOC-BAR-ARC > SPACES
                 MOVE '1'                   TO Y00ODETF-IDC-ARC
                 MOVE Y5FNLN-NO-DOC-BAR-ARC TO Y00ODETF-NO-DOC-BAR-ARC
              ELSE
                 MOVE '0'                   TO Y00ODETF-IDC-ARC
              END-IF
      *      ---
              IF Y5FNLN-CD-TY-DOC = 'ADH'
                  MOVE 'adhesion/'  TO WS-ADRESSE
              ELSE
                  MOVE 'decision/'  To WS-ADRESSE
              END-IF
              MOVE 27920 TO Y00ODETF-VLR-LG-FLX-EDI
              STRING WS-URL-WEBSTORE(1:WS-IND-NON-SPACE-FIN)
                         Y5FNLN-CD-PTN '/'
                         WS-ADRESSE
              DELIMITED BY SIZE INTO Y00ODETF-VLR-SVR
              MOVE WS-NBRE-E1-LUS TO WS-NUM-EDI
              STRING 'TEMP' WS-NUM-EDI WS-DATE-OPC'.pdf'
              DELIMITED BY SIZE INTO Y00ODETF-LIB-FIC
              MOVE Y00ODETF-ENR-ENTETE TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

      *-------PREPARATION DE LA PREMIERE LIGNE DU FLUX SEPIAS
              MOVE SPACE TO Y00ODETF-CD-ENR-F
              MOVE ENR-E1-DATA (1:5000) TO Y00ODETF-FLUX
              MOVE Y00ODETF-ENR-DETAIL TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

      *-------PREPARATION DE LA SECONDE LIGNE DU FLUX SEPIAS
              MOVE ENR-E1-DATA (5001:5000) TO Y00ODETF-FLUX
              MOVE Y00ODETF-ENR-DETAIL TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

      *-------PREPARATION DE LA TROISIEME LIGNE DU FLUX SEPIAS
              MOVE ENR-E1-DATA (10001:5000) TO Y00ODETF-FLUX
              MOVE Y00ODETF-ENR-DETAIL TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

      *-------PREPARATION DE LA QUATRIEME LIGNE DU FLUX SEPIAS
              MOVE ENR-E1-DATA (15001:5000) TO Y00ODETF-FLUX
              MOVE Y00ODETF-ENR-DETAIL TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

      *-------PREPARATION DE LA CINQUIEME LIGNE DU FLUX SEPIAS
              MOVE ENR-E1-DATA (20001:5000) TO Y00ODETF-FLUX
              MOVE Y00ODETF-ENR-DETAIL TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

      *-------PREPARATION DE LA DERNIERE LIGNE DU FLUX SEPIAS
              MOVE 'FN' TO Y00ODETF-CD-ENR-F
              MOVE ENR-E1-DATA (25001:2285) TO Y00ODETF-FLUX
              MOVE Y00ODETF-ENR-DETAIL TO ENR-S1
              PERFORM D-ECR-S1 THRU F-ECR-S1

           END-IF.

              PERFORM D-LECT-E1     THRU F-LECT-E1.

       F-TRAITEMENT.
           EXIT.
      *
      *
       D-LECT-E1.
      *------------------------------------------------------------
      *              LECTURE FICHIER E1 EN ENTREE
      *------------------------------------------------------------
           IF NOT FE1
              READ FIC-E1 INTO Y5FNLN-Y5FNLEX
              AT END
                 SET FE1            TO TRUE
                 MOVE HIGH-VALUE TO ENR-E1
                 MOVE HIGH-VALUE TO Y5FNLN-Y5FNLEX
              NOT AT END
                 ADD 1               TO WS-NBRE-E1-LUS
              END-READ
            END-IF.
      *
       F-LECT-E1.
           EXIT.
      *

       D-ECR-S1.
      *------------------------------------------------------------
      *              ECRITURE FICHIER  S1
      *------------------------------------------------------------
           WRITE     ENR-S1
           ADD   1   TO WS-NBRE-S1-ECR
           .
      *
       F-ECR-S1.
           EXIT.
      *
      *
       D-FERMETURE.
      *------------------------------------------------------------
      *              FERMETURE  FICHIERS
      *------------------------------------------------------------
           CLOSE  FIC-E1
                  FIC-S1.
      *
       F-FERMETURE.
           EXIT.

      *=============================================================*
      *  FIN ANORMALE DU TRAITEMENT                                 *
      *=============================================================*

       PB-TRT12.
      *=========
      *    IF SQLCODE NOT = ZERO
      *       MOVE SQLCODE  TO WS-SQLCODE
      *       DISPLAY '       -----> SQLCODE = ' WS-SQLCODE
      *    END-IF.
           MOVE 12  TO RETURN-CODE.
           PERFORM  D-FERMETURE
              THRU  F-FERMETURE.
           STOP RUN.

       PB-TRT8.
      *=======
           MOVE 8   TO RETURN-CODE.
           PERFORM  D-FERMETURE
              THRU  F-FERMETURE.
           STOP RUN.


       END PROGRAM P5FE03.
